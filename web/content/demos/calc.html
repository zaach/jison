---
title: Jison - calculator demo page
---


    <link href="../../assets/styles/try.css" type="text/css" rel="stylesheet" media="screen" />


      <h1>Calculator demo</h1>
      <p>This demo parses mathematical expressions and returns the answer, keeping the correct order of operations. </p>
      <p>Enter an expression to evaluate, such as PI*4^2 + 5:</p>
      <p>
        <input type="text" value="PI*4^2 + 5" id="value_to_calculate" /> <button id="do_calculate">equals</button> <span id="calculated_value"></span>
      </p>

      <h2>The grammar</h2>
      <p>
      This Jison grammar was used to create the parser/evaluator:
      </p>

      <pre>
/* description: Parses and executes mathematical expressions. */

/* lexical grammar */
%lex
%%

\s+                   /* skip whitespace */
[0-9]+("."[0-9]+)?\b  return 'NUMBER';
"*"                   return '*';
"/"                   return '/';
"-"                   return '-';
"+"                   return '+';
"^"                   return '^';
"!"                   return '!';
"%"                   return '%';
"("                   return '(';
")"                   return ')';
"PI"                  return 'PI';
"E"                   return 'E';
&lt;&lt;EOF>>               return 'EOF';
.                     return 'INVALID';

/lex

/* operator associations and precedence */

%left '+' '-'
%left '*' '/'
%left '^'
%right '!'
%right '%'
%left UMINUS
%token INVALID

%start expressions

%% /* language grammar */

expressions
    : e EOF
        { typeof console !== 'undefined' ? console.log($1) : print($1);
          return $1; }
    ;

e
    : e '+' e
        {$$ = $1 + $3;}
    | e '-' e
        {$$ = $1 - $3;}
    | e '*' e
        {$$ = $1 * $3;}
    | e '/' e
        {$$ = $1 / $3;}
    | e '^' e
        {$$ = Math.pow($1, $3);}
    | e '!'
        {{
          $$ = (function fact(n) { return n == 0 ? 1 : fact(n - 1) * n; })($1);
        }}
    | e '%'
        {$$ = $1 / 100;}
    | '-' e %prec UMINUS
        {$$ = -$2;}
    | '(' e ')'
        {$$ = $2;}
    | NUMBER
        {$$ = Number(yytext);}
    | E
        {$$ = Math.E;}
    | PI
        {$$ = Math.PI;}
    ;
    </pre>



    <script src="../../assets/js/jquery.js"></script>
    <script src="../../assets/js/calculator.js"></script>
    <script>
      $(document).ready(function () {
        $("#do_calculate").click(function () {
          try {
            var result = calculator.parse($("#value_to_calculate").val());
            $("#calculated_value").html(result);
          } catch (e) {
            $("#calculated_value").html(String(e));
          }
        });
      });
    </script>
